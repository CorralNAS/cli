# Test runner for vm category
#

# State variables shared between create and destroy functions.
_vm_name="vm_test_image"
_vm_template="freebsd-10.3-zfs"
_vm_object=null
_vm_description="This is a test"
_vm_hard_count = 5
_vm_hard_arr = []

function vm_create() {
	print(">>> Starting vm tests")
	_vm_object = @$(vm create name=${_vm_name} template=${_vm_template} volume=${VOL_NAME})
	maybe_barf(_success, "+vm#1", "Unable to create VM " + _vm_name)
	maybe_barf($(_vm_object get cores) == 1, "+vm#2", "Wrong number of cores returned for VM " + _vm_name)
	@$(_vm_object set description=${_vm_description})
	_x = $(_vm_object get description)
	maybe_barf(_x == _vm_description, "+vm#3", "Description for " + _vm_name + " didn't match expected: " + _vm_description)
	@$(_vm_object start)
	maybe_barf(_success, "+vm#4", "Unable to start VM " + _vm_name)
	@$(_vm_object stop)
	maybe_barf(_success, "+vm#4", "Unable to stop VM " + _vm_name)
}

function vm_destroy() {
	 print(">>> Finishing vm tests")
	 @$(_vm_object delete)
	maybe_barf(_success, "-vm#1", "Unable to delete VM " + _vm_name)
}

function vm_hard_create() {
	printf(">>> Starting vm stress tests: %d virtual machines\n", _vm_hard_count)
	for (i in mapf(range(0, _vm_hard_count), _vm_name + "%d")) {
	    _x = @$(vm create name=${i} template=${_vm_template} volume=${VOL_NAME})
	    if (not (maybe_barf(_success, "+vm-hard#1", "Unable to create VM " + i))) {
	       append(_vm_hard_arr, _x)
	       @$(_x start)
	       maybe_barf(_success, "+vm-hard#2", "Unable to start VM " + i)
	       @$(_x stop)
	       maybe_barf(_success, "+vm-hard#3", "Unable to stop VM " + i)
	    }
	    
	}
	_l = count_by_name("vm", _vm_name + "[0-9]+")
	maybe_barf(_l == _vm_hard_count, "+vm-hard#2", "VM count of " + str(_l) + " does not match " + str(_vm_hard_count))
}

function vm_hard_destroy() {
	print(">>> Finishing vm stress tests")
	for (i in _vm_hard_arr) {
	    _n = $(i get name)
	    @$(i delete)
	    maybe_barf(_success, "-vm-hard#1", "Unable to delete VM " + _n)
	}
	_l = count_by_name("vm", _vm_name + "[0-9]+")
	maybe_barf(_l == 0, "-vm-hard#2", "Unable to delete all VMs: " + str(_l) + " remaining.")
}
